%class "LangParser";
%package "lang.ast";

%embed {:
	static public class SyntaxError extends RuntimeException { public SyntaxError(String msg) {super(msg);}}
	// Disable syntax error recovery
	protected void recoverFromError(Symbol token, TokenStream in) {
		throw new SyntaxError("Cannot recover from the syntax error");
	}
:};

%terminals ID, NUMERAL,  INT, ASSIGN, SEMI, RETURN, COMMA, IF, WHILE, ELSE,
    ADD, MUL, DIV, MIN, MOD, LE, LT, GE, GT, EQ, NEQ,
    LBRACE, RBRACE, LPARENTHESES, RPARENTHESES,
    READ, PRINT;

%typeof program = "Program";
%typeof func_list = "List";
%typeof func_id = "FunID";
%typeof func = "Func";
%typeof assign = "Assign";
%typeof var_decl = "VarDecl";
%typeof id_use = "IdUse";
%typeof id_decl = "IdDecl";
%typeof id_para = "IdPara";
%typeof expr = "Expr";
%typeof stmt = "Stmt";
%typeof stmts = "Stmts";
%typeof stmt_opt = "Opt";
%typeof stmt_list = "List";
%typeof decl_opt = "Opt";
%typeof number = "Numeral";
%typeof arithmetic = "Expr";
%typeof add = "Expr";
%typeof mul = "Expr";
%typeof factor = "Expr";
%typeof comparison = "Expr";
%typeof ret = "Return";
%typeof paras_opt = "Opt";
%typeof paras_list = "List";
%typeof paras = "Paras";
%typeof para = "Para";
%typeof if_stmt = "IfStmt";
%typeof while_stmt = "WhileStmt";
%typeof else_opt = "Opt";
%typeof call = "Expr";
%typeof pass_opt = "Opt";
%typeof pass_list = "List";
%typeof print = "Stmt";
%typeof read = "Expr";

%goal program;

program = func_list.a {: return new Program(a); :} ;

func_list =
    func.a {: return new List().add(a); :}
    |   func_list.a func.b {: return a.add(b); :} ;

func =
    INT func_id.a LPARENTHESES paras_opt.b RPARENTHESES LBRACE stmt_opt.c RBRACE{: return new Func(a,b,c); :} ;

paras_opt =
    /* epsilon */ {: return new Opt(); :}
    |   paras.a {: return new Opt(a); :};

paras =
    paras_list.a {: return new Paras(a); :};

paras_list =
    para.a {: return new List().add(a); :}
    |   paras_list.a COMMA para.b {: return a.add(b); :};

para =
    INT id_para.a {: return new Para(a); :};


stmt_opt =
    /* epsilon */ {: return new Opt(); :}
    |   stmts.a {: return new Opt(a); :};

stmts =
    stmt_list.a {: return new Stmts(a); :};

stmt_list =
    stmt.a {: return new List().add(a); :}
    |   stmt_list.a stmt.b {: return a.add(b); :};

stmt =
    assign
    |   var_decl
    |   ret
    |   if_stmt
    |   while_stmt
    |   print;

assign =
    id_use.a ASSIGN expr.b SEMI {: return new Assign(a,b); :};

var_decl =
    INT id_decl.a decl_opt.b SEMI {: return new VarDecl(a,b); :};

ret =
    RETURN expr.a SEMI {: return new Return(a); :};

if_stmt =
    IF LPARENTHESES expr.a RPARENTHESES LBRACE stmt_list.b RBRACE else_opt.c {: return new IfStmt(a,b,c); :};

else_opt =
    /* epsilon */ {: return new Opt(); :}
    |   ELSE LBRACE stmts.a RBRACE {: return new Opt(a); :};

while_stmt =
    WHILE LPARENTHESES expr.a RPARENTHESES LBRACE stmt_list.b RBRACE {: return new WhileStmt(a,b); :};

decl_opt =
    /* epsilon */ {: return new Opt(); :}
    |   ASSIGN expr.a {: return new Opt(a); :};

print =
    PRINT LPARENTHESES expr.a RPARENTHESES SEMI {: return new Print(a); :};

expr =
    arithmetic
    |   comparison
    |   call
    |   read;

comparison =
    arithmetic.a EQ arithmetic.b {: return new Eq(a,b); :}
    |   arithmetic.a NEQ arithmetic.b {: return new Neq(a,b); :}
    |   arithmetic.a LE arithmetic.b {: return new Le(a,b); :}
    |   arithmetic.a LT arithmetic.b {: return new Lt(a,b); :}
    |   arithmetic.a GE arithmetic.b {: return new Ge(a,b); :}
    |   arithmetic.a GT arithmetic.b {: return new Gt(a,b); :};

arithmetic =
    add;

add =
    add.a ADD mul.b {: return new Add(a,b); :}
    |   add.a MIN mul.b {: return new Min(a,b); :}
    |   mul;

mul =
    mul.a MUL factor.b {: return new Mul(a,b); :}
    |   mul.a DIV factor.b {: return new Div(a,b); :}
    |   mul.a MOD factor.b {: return new Mod(a,b); :}
    |   factor;

factor =
    id_use
    |   number
    |   LPARENTHESES expr.a RPARENTHESES {: return a; :};

number =
    NUMERAL.num {: return new Numeral(num); :};

call =
    func_id.a LPARENTHESES pass_opt.b RPARENTHESES {: return new Call(a,b); :};

pass_opt =
    /* epsilon */ {: return new Opt(); :}
    |   pass_list.a {: return new Opt(a); :};

pass_list =
    expr.a {: return new List().add(a); :}
    |   pass_list.a COMMA expr.b {: return a.add(b); :};

read =
    READ LPARENTHESES pass_opt.a RPARENTHESES {: return new Read(a); :};

func_id = ID.id {: return new FunID(id); :} ;
id_use = ID.id {: return new IdUse(id); :} ;
id_decl = ID.id {: return new IdDecl(id); :};
id_para = ID.id {: return new IdPara(id); :};